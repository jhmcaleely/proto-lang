var pico_led = 0d25;

var GPIO_OUT = true;
var GPIO_IN = false;
var GPIO_FUNC_SIO = 0d5;

var PADS_BANK0_GPIO0_IE_BITS = 0x00000040;
var PADS_BANK0_GPIO0_OD_BITS = 0x00000080;
var PADS_BANK0_GPIO0_ISO_BITS = 0x00000100;

var sio_hw = 0xd0000000;
var gpio_out_offset = 0x10;
var gpio_oe_offset = 0x20;


class alias_reg {
    init(addr, stride) { this.base = addr; this.stride = stride; }

    read() { return rpeek(this.base); }
    write(val) { rpoke(this.base, val); }
    write_masked(val, mask) { this.xor((this.read() ^ val) & mask); }

    set(mask) { rpoke(this.base + 0d1 * this.stride, mask); }
    clr(mask) { rpoke(this.base + 0d2 * this.stride, mask); }
    xor(mask) { rpoke(this.base + 0d3 * this.stride, mask); }
}

fun gpio_set_direction(pin, dir) {
    var ctrl = alias_reg(sio_hw + gpio_oe_offset, 0d4);

    if (dir) {
        ctrl.set(0x1 << pin);
    }
    else {
        ctrl.clr(0x1 << pin);
    }
}

fun gpio_put(pin, value) {
    var ctrl = alias_reg(sio_hw + gpio_out_offset, 0d4);

    if (value) {
        ctrl.set(0x1 << pin);
    }
    else {
        ctrl.clr(0x1 << pin);
    }
}

fun gpio_init(pin) {
    gpio_set_direction(pin, GPIO_IN);
    gpio_put(pin, false);
    gpio_set_function(pin, GPIO_FUNC_SIO);
}

var PADS_BANK0_BASE = 0x4001c000;
var IO_BANK0_BASE   = 0x40014000;

fun gpio_set_function(pin, function) {

    var pads_bank_0_pin_offset = 0d4 + 0d4 * pin;
    var pinctrl = alias_reg(PADS_BANK0_BASE + pads_bank_0_pin_offset, 0d4096);

    // Set input enable on, output disable off
    pinctrl.write_masked(PADS_BANK0_GPIO0_IE_BITS, PADS_BANK0_GPIO0_IE_BITS | PADS_BANK0_GPIO0_OD_BITS);

    // Zero all fields apart from fsel; we want this IO to do what the peripheral tells it.
    // This doesn't affect e.g. pullup/pulldown, as these are in pad controls.
    var ctrl_offset = (0d8 * pin) + 0d4;
    var hw_ctrl = alias_reg(IO_BANK0_BASE + ctrl_offset, 0d4096);
    hw_ctrl.write(function);
}

gpio_init(pico_led);
gpio_set_direction(pico_led, GPIO_OUT);

gpio_put(pico_led, true);
